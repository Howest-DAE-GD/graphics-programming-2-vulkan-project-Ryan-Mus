#version 450

layout(binding = 0, rgba32f) uniform readonly image2D inputImage;
layout(binding = 1, rgba8)   uniform writeonly image2D outputImage;

layout(local_size_x = 16, local_size_y = 16) in;

// ACES tone mapping constants
const float A = 2.51f;
const float B = 0.03f;
const float C = 2.43f;
const float D = 0.59f;
const float E = 0.14f;

// Push constants for camera exposure settings
layout(push_constant) uniform ExposureSettings {
    float aperture;
    float ISO;
    float shutterSpeed;
} exposure;

// -------- Exposure Utilities --------
float CalculateEV100FromPhysicalCamera(in float aperture, in float shutterTime, in float ISO)
{
    // EV100 = log2((aperture^2) / shutter * 100 / ISO)
    return log2(pow(aperture, 2.0) / shutterTime * (100.0 / ISO));
}

float ConvertEV100ToExposure(in float EV100)
{
    // Based on Lagarde's implementation
    const float maxLuminance = 1.2f * pow(2.0f, EV100);
    return 1.0f / max(maxLuminance, 0.0001f);  // Prevent divide-by-zero
}

// -------- ACES Tone Mapping --------
vec3 ACESFilm(vec3 x)
{
    return clamp((x * (A * x + B)) / (x * (C * x + D) + E), 0.0, 1.0);
}

void main()
{
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 imageSize = imageSize(inputImage);
    if (texelCoord.x >= imageSize.x || texelCoord.y >= imageSize.y)
        return;

    vec4 inputColor = imageLoad(inputImage, texelCoord);

    float EV100     = CalculateEV100FromPhysicalCamera(exposure.aperture, exposure.shutterSpeed, exposure.ISO);
    float exposureValue = ConvertEV100ToExposure(EV100);

    vec3 exposedColor = inputColor.rgb * exposureValue;
    vec3 mappedColor = ACESFilm(exposedColor);

    imageStore(outputImage, texelCoord, vec4(mappedColor, inputColor.a));
}